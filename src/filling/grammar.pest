main = {
  SOI
  ~ def+
  ~ EOI
}
def = { name ~ "=" ~ exp }
exp = { tree ~ ("where" ~ conn+)? }
conn = { tree ~ "~" ~ tree }
tree = { node | name }
node = { era | ref | num | con | dup | opr | swi }
era = { "*" ~ name? }
ref = { "@" ~ name }
con = { "(" ~ tree ~ tree ~ ")" }
dup = { "{" ~ tree ~ tree ~ "}" }
opr = { "$(" ~ tree ~ tree ~ ")" }
swi = { "?(" ~ tree ~ tree ~ ")" }
num = { u8 | opn }
u8 = @{ NUMBER+ }
opn = { "[" ~ (lop | rop | oprev | prim) ~ "]" }
lop = { u8 ~ prim }
rop = { prim ~ u8 }
oprev = { ":" ~ prim }
prim = { "+" | "-" | "*" | "/" }

name = @{ char+  }
char = { ASCII_ALPHANUMERIC | "_" | "/" | "-" | "'" }
WHITESPACE = _{ " " | NEWLINE }
COMMENT = _{block | line}
block = { "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
line = { "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }